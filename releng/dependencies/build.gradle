plugins {
  id "org.standardout.bnd-platform" version "1.7.0"
  id "com.github.ben-manes.versions" version "0.21.0"
}

repositories {
	jcenter()
}

platform {
	categoryId = 're4fx-Libs'
	categoryName = 're4fx Libs'

	generatePlatformFeature = false

	defaultQualifier = ''
	useBndHashQualifiers = false
	useFeatureHashQualifiers = false
	defaultQualifierMap.prefix = ''

	feature(id: 'controlsfx', name: 'ControlsFx', version: '11.0.0') {
		plugin('org.controlsfx:controlsfx:11.0.0') {
			bnd {
				// fix manifest problems due to e(fx)clipse class path resolution 
				instruction 'Import-Package', 'javax.inject;version="1.0.0"'
				instruction 'Require-Capability', 'osgi.ee; filter="(&(osgi.ee=JavaSE)(version=11))"'
				instruction 'Java-Module-AddExports', 'javafx.graphics/javafx.scene=.,javafx.graphics/javafx.css=.,javafx.controls/javafx.scene.control=.,javafx.base/javafx.event=.,javafx.base/javafx.collections=.,javafx.graphics/javafx.css=.,javafx.graphics/com.sun.javafx.scene=.,javafx.graphics/javafx.scene=.,javafx.graphics/com.sun.javafx.scene.traversal=.,javafx.graphics/com.sun.javafx.css=.,javafx.controls/com.sun.javafx.scene.control.behavior=.,javafx.controls/com.sun.javafx.scene.control=.,javafx.controls/com.sun.javafx.scene.control.inputmap=.,javafx.base/com.sun.javafx.event=.,javafx.base/com.sun.javafx.collections=.,javafx.base/com.sun.javafx.runtime=.,javafx.graphics/com.sun.javafx.css=.,javafx.graphics/com.sun.javafx.application=.,javafx.graphics/com.sun.glass.ui=.'
			}
		}
	}

	feature(id: 'jackson', name: 'Jackson', version: '2.9.8') {
		plugin('com.fasterxml.jackson.core:jackson-databind:2.9.8')
	}

	feature(id: 'mockito', name: 'Mockito', version: '2.27.0') {
		plugin('org.mockito:mockito-core:2.27.0')
	}

	feature(id: 'zt-exec', name: 'zt-exec', version: '1.10') {
		plugin('org.zeroturnaround:zt-exec:1.10')
	}

	feature(id: 'Project-Reactor', name: 'Project Reactor', version: '3.2.8') {
		plugin('io.projectreactor:reactor-core:3.2.8.RELEASE') {
			bnd {
				optionalImport 'javax.swing'
				optionalImport 'kotlin'
				optionalImport 'kotlin.jvm'
				optionalImport 'kotlin.jvm.functions'
				optionalImport 'kotlin.jvm.internal'
				optionalImport 'kotlin.collections'
				optionalImport 'kotlin.jvm.internal.markers'
				optionalImport 'kotlin.reflect'
				optionalImport 'kotlin.sequences'
				optionalImport 'sun.misc'
				optionalImport 'io.micrometer.core.instrument'
				optionalImport 'io.micrometer.core.instrument.composite'
				optionalImport 'javax.swing'
			}
		}

		plugin('io.projectreactor.addons:reactor-extra:3.2.2.RELEASE') {
			bnd {
				optionalImport 'javax.swing'
				optionalImport 'kotlin'
				optionalImport 'kotlin.jvm.functions'
				optionalImport 'kotlin.jvm.internal'
			}
		}

		plugin('io.projectreactor:reactor-test:3.2.8.RELEASE') {
			bnd {
				optionalImport 'kotlin'
				optionalImport 'kotlin.jvm'
				optionalImport 'kotlin.jvm.internal'
				optionalImport 'kotlin.reflect'
			}
		}
	}
	
	feature(id: 'reactor-netty', name: 'Reactor Netty', version: '0.8.6') {
		plugin('io.projectreactor.netty:reactor-netty:0.8.6.RELEASE')
	}

	feature(id: 'reactorfx', name: 'ReactorFx', version: '2.0.0') {
		plugin('com.github.shadskii:reactorfx:2.0.0') {
			bnd {
				instruction 'Import-Package', 'freetimelabs.io.reactorfx.schedulers;version="[2.0,3)", reactor.core,reactor.core.publisher,reactor.core.scheduler'
			}
		}
	}
	
	feature(id: 'RSocket', name: 'RSocket', version: '0.12.1') {
	   plugin('io.rsocket:rsocket-core:0.12.1')
	   plugin('io.rsocket:rsocket-transport-netty:0.12.1')
	}

	feature(id: 'jcommander', name: 'jcommander', version: '1.74.0') {
			plugin('com.beust:jcommander:1.74')
	}
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

wrapper {
	gradleVersion = '5.4.1'
}
